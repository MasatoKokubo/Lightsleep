// Copyright (c) 2016 Masato Kokubo

apply plugin: 'java'
apply plugin: 'eclipse'

repositories {
    mavenCentral()
}

dependencies {
//	compile 'org.apache.logging.log4j:log4j-api:2.3'
//	compile 'log4j:log4j:1.2.17'
//	compile 'org.slf4j:slf4j-api:1.7.12'

	compile files(
		'../../DebugTrace/DebugTrace/build/libs/debugtrace-1.8.0.jar',
		'C:/Java/apache-log4j-2.3-bin/log4j-1.2-api-2.3.jar',
		'C:/Java/apache-log4j-2.3-bin/log4j-api-2.3.jar',
		'C:/Java/slf4j-1.7.12/slf4j-api-1.7.12.jar',
		'C:/Java/jdbc/ojdbc7_g.jar'
	)
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
version             = '1.0.0'
project.archivesBaseName = 'lightsleep'

compileJava {
	options.encoding     = 'UTF-8'
	options.deprecation  = true
	options.debug        = true
	options.compilerArgs = ['-Xlint']
}

task compileJava_ja(type:JavaCompile) {
	options.encoding     = compileJava.options.encoding
	options.deprecation  = compileJava.options.deprecation
	options.debug        = compileJava.options.debug
	options.compilerArgs = compileJava.options.compilerArgs

	classpath      = compileJava.classpath
	source         = files('src/main_ja/java')
	destinationDir = file('build/classes/main_ja')
}

jar {
	manifest {
		attributes	'Implementation-Title'    : project.name,
					'Implementation-Version'  : version,
					'Implementation-Vendor'   : 'Masato Kokubo',
					'Implementation-Vendor-Id': 'org.mkokubo'
	}
}

javadoc {
	title               = "$project.name $version API Specification"
	options.encoding    = 'UTF-8'
	options.locale      = 'en'
	options.windowTitle = "$project.name"
	options.overview    = 'src/main/java/overview.html'
	options.addBooleanOption('author', true)
	options.addStringOption ('bottom', '<div class="copyright">&copy;2016 Masato Kokubo</div>')
}

javadoc.doFirst {
	def cssFile = file("build/docs/javadoc/stylesheet.css")
	if (cssFile.exists())
		cssFile.delete()
}

javadoc.doLast {
	def cssFile = file("build/docs/javadoc/stylesheet.css")
	file("src/main/java/addtional.css").eachLine {
		cssFile.append("$it\n")
	}
}

task javadoc_ja(type:Javadoc) {
	dependsOn = ['compileJava_ja']

	title               = "$project.name $version API 仕様"
	options.encoding    = javadoc.options.encoding
	options.locale      = 'ja'
	options.windowTitle = javadoc.options.windowTitle
	options.overview    = 'src/main/java/overview_ja.html'
	options.addBooleanOption('author', true)
	options.addStringOption ('bottom', '<div class="copyright">&copy;2016 Masato Kokubo</div>')

	classpath      = javadoc.classpath
	source         = fileTree('src/main_ja/java').include('**/*.java')
	destinationDir = file('build/docs/javadoc_ja')
}

javadoc_ja.doFirst {
	def cssFile = file("build/docs/javadoc_ja/stylesheet.css")
	if (cssFile.exists())
		cssFile.delete()
}

javadoc_ja.doLast {
	def cssFile = file("build/docs/javadoc_ja/stylesheet.css")
	file("src/main/java/addtional_ja.css").eachLine {
		cssFile.append("$it\n")
	}
}

task zip(type: Zip) {
	classifier = 'bin'

	into('/'     , {from 'build/libs'})
	into('/'     , {from 'LICENSE.txt', 'README.txt'})
	into('doc'   , {from 'build/docs/javadoc'})
	into('doc_ja', {from 'build/docs/javadoc_ja'})
	into('resources-sample', {from 'src/test/resources'})
}
